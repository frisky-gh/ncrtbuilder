#!/usr/bin/perl

use strict;

if( @ARGV < 5 ){
        print "usage: % $0 CONFDIR WORKDIR MEASURE HOST SERVICE\n";
        exit 1;
}

our $CONFDIR = $ARGV[0];
our $WORKDIR = $ARGV[1];
our $MEASURE = $ARGV[2];
our $VHOST = $ARGV[3];
our $VSERVICE = $ARGV[4];

####
sub ns ($) {
	return sprintf "%.2fs", $_[0];
}

sub nB ($) {
	return sprintf "%dB", $_[0];
}

#### load service conf.
my %rules;
my $f = "$CONFDIR/agentless/webactivity.conf.$MEASURE.$VHOST";
open my $h, '<', $f or do {
	die "$f: cannot open, stopped";
};
while( <$h> ){
	chomp;
	next if m"^\s*($|#)";
	die "$f:$.: illegal format, stopped" unless m"^
		(\S+)
		\s+
		http(s)?://
		(?:([^\s:]+):([^\s\@]*)\@)?
		([-0-9a-zA-Z.]+)
		(?::(\d+))?
		((/[^\s\?]*)?(?:\?(\S*))?)
		\s+
		(\d+)
		(  (?: \s+ \S+ )*  )
		\S*
	$"x;
	my $vservicename = $1;
	my $secure = $2;
	my $user = $3;
	my $passwd = $4;
	my $host = $5;
	my $port = $6 || ($secure ? 443 : 80);
	my $path_query = $7;
	my $path = $8;
	my $query = $9;
	my $status = $10;
	my $options = $11;
	my $args = "-H $host -p $port";
	$args .= " -S" if $secure;
	$args .= " -a \Q$user:$passwd\E" if $user;
	$args .= " -u \Q$path_query\E" if $path_query;
	if( $options ){
		foreach my $option ( split m"\s+", $options ){
			$args .= " \Q$option\E";
		}
	}
	# same url string to the influx_url in build_ncrtmaster_website_activity
	my $url = $secure ? "https" : "http";
	$url .= "://";
	$url .= "$user\@" if $user;
	$url .= $host;
	$url .= ":$port" if $port;
	$url .= $path_query if $path_query;
	$rules{$vservicename} = [$url, $status, $args, $vservicename];
}
close $h;

####
my ($url, $wantstatus, $args, $vservicename) = @{$rules{$VSERVICE} // []};
unless( defined $url ){
	print "web[$VSERVICE]-status=2\n";
	exit 1;
}

my %d;
my $f = "/usr/lib/nagios/plugins/check_http";
open my $h, '-|', "$f $args" or do {
	die "$f: cannot execute, stopped";
};
my $r = <$h>;
close $h;
chomp $r;
$r =~ m"^
	HTTP\ (
	    (?:
		(OK|WARNING|CRITICAL):
		\ HTTP/\S+\ (\d+)\ (?:(.*)\ )?-\ (.*)
		\|time=(\d+\.\d+)s\;.*\ size=(\d+)B\;.*
	    ) |
	    CRITICAL\ -\ (.*)
	)
$"x or do {
	print "web[$vservicename]-size=-1\n";
	print "web[$vservicename]-elapsed=-1\n";
	print "web[$vservicename]-status=2\n";
	open my $h, '>>', "$WORKDIR/webactivity.log";
	print $h time, " ERROR: url=$url response=$r\n";
	close $h;
	exit 0;
};
my $result = $2;
my $statuscode = $3;
my $statusmessage = $4 || $8;
my $message = $5;
my $elapsed = ns $6;
my $size = nB $7;
my $status = $statuscode eq $wantstatus ? 0 : 1;
print "web[$vservicename]-size=$size\n";
print "web[$vservicename]-elapsed=$elapsed\n";
print "web[$vservicename]-status=$status\n";

exit 0;

