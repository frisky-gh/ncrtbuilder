#!/usr/bin/perl

use strict;
use IPC::Open2;

$0 =~ m"^(.*)/";
our $TOOLHOME = "$1/..";
while( $TOOLHOME =~ s{(^|/)[^\.][^/]*/\.\.(/|$)}{$1} ){}
our $BINDIR = "$TOOLHOME/bin";
our $PLUGINSDIR = "$TOOLHOME/plugins";
our $CONFDIR = "$TOOLHOME/conf";

if( @ARGV < 4 ){
	print "usage: % $0 SERVICE VHOST VSERVICE PERFDATA ...\n";
	exit 3;
}
my $service = $ARGV[0];
my $vhost = $ARGV[1];
my $vservice = $ARGV[2];

my $import_host;
my $import_service;
my $import_prefix;
my %import;
foreach my $i ( splice @ARGV, 3 ){
	foreach my $j ( split m"\s+", $i ){
		if    ( $j =~ m"^([^\s=:]+):([^\s=:]+):([^\s=:]*)$" ){
			$import_host = $1;
			$import_service = $2;
			$import_prefix = $3;
		}elsif( $j =~ m"^([^\s=]+)=([-+]?\d+(\.\d+)?)(\w{1,2}|%)?(;\S+)?$" ){
			my $key = $1;
			my $value = $2;
			$import{"$import_prefix$key"} = $value;
		}else{
			print "parse error: \"$j\", stopped\n";
			exit 3;
		}
	}
}

# setup work directory
our $WORKDIR;
if( $ENV{'HOME'} ){ $WORKDIR = $ENV{'HOME'} . "/.ncrt"; }
else{
	my ($name, $passwd, $uid, $gid, $quota, $comment, $gcos, $home, $shell, $expire ) = getpwuid $<;
	$WORKDIR = "$home/.ncrt";
}
unless( -d $WORKDIR ){ mkdir $WORKDIR or die "$WORKDIR: cannot create, stopped"; }

# metrics
my %m;
#
my @w;
my @c;
my @r;

# read from service's output
my $f = "$PLUGINSDIR/ncrtmastercomposite_$service";
open2 my $out, my $in, "$f $CONFDIR $WORKDIR $vhost $vservice" or do {
	print "UNKNOWN $f: not found.\n";
	exit 3;
};
while( my ($k, $v) = each %import ){
	print $in "$k=$v\n";
}
close $in;
while( <$out> ){
	chomp;
	next if m"^\s*(#|$)";
	die "$_, stopped" unless m"^([-\w/\[\].:@\$%#\*]+)=(\S+)$";
	my $k = $1;
	my $v = $2;
	if( $k eq 'output' ){ push @r, $v; }
	else                { $m{$k} = $v; }
}
close $out;

my $plugin_has_failed;
my $plugin_rc = $? >> 8;
if( $plugin_rc > 0 ){
	$plugin_has_failed = 1;
}

# thresholds
my @wt;
my @ct;
# load thresholds
my $f = "$CONFDIR/threshold/thresholds.$vhost.$vservice";
if( open my $h, '<', $f ){
	while( <$h> ){
		chomp;
		next if m"^\s*(#|$)";
		die unless m"^(\S+)\s+(crit|warn)\s+\[\s*([-+]?\d+(?:\.\d+)?)\s*,\s*([-+]?\d+(?:\.\d+)?)\s*\]$";
		my $itempattern = qr"^$1$";
		my $severity = $2;
		my $lower = $3;
		my $upper = $4;
		if    ( $severity eq 'crit' ){
			push @ct, [ $itempattern, $lower, $upper ];
		}elsif( $severity eq 'warn' ){
			push @wt, [ $itempattern, $lower, $upper ];
		}
	}
	close $h;
}

# threshold per metric
my %wm;
my %cm;

# select thresholds
foreach my $k ( keys %m ){
	foreach my $t ( @wt ){
		my ($re, $lower, $upper) = @$t;
		next unless $k =~ $re;
		$wm{$k} = [$lower, $upper];
	}
	foreach my $t ( @ct ){
		my ($re, $lower, $upper) = @$t;
		next unless $k =~ $re;
		$cm{$k} = [$lower, $upper];
	}
}

#
my @p;
foreach my $k ( sort keys %m ){
	my $v = $m{$k};
	my $c;
	my $critical_occurred;
	my ($min, $max);
	
	next unless $v =~ m"^([-+]?\d+(?:\.\d+)?)(.*)$";
	my $value = $1;
	my $unit = $2;
	if   ( $unit eq '%' ) { $min = '0.00'; $max = '100.00'; }
	elsif( $unit eq 'MB' ){ $min = '0.00'; }

	my $key_text = $unit ? "$k\[$unit\]" : "$k";

	if( $cm{$k} ){
		my ($lower, $upper) = @{$cm{$k}};
		$c = "$lower:$upper";
		if    ( $v < $lower ){
			push @c, "$key_text:${value}(lower-crit-thr:$lower)";
			$critical_occurred = 1;
		}elsif( $v > $upper ){
			push @c, "$key_text:${value}(upper-crit-thr:$upper)";
			$critical_occurred = 1;
		}
	}
	my $w;
	if( $wm{$k} ){
		my ($lower, $upper) = @{$wm{$k}};
		$w = "$lower:$upper";
		if    ( $critical_occurred ){
			# ignore
		}elsif( $v < $lower ){
			push @w, "$key_text:${value}(lower-warn-thr:$lower)";
		}elsif( $v > $upper ){
			push @w, "$key_text:${value}(upper-warn-thr:$upper)";
		}
	}
	my $p = "$k=$v;$w;$c;$min;$max";
	push @p, $p;
}

#
my $r = 'OK';
my $rc = 0;
if( $plugin_has_failed ){
	$r = 'CRIT';
	$rc = 2;
}
elsif( @c ){ $r = 'CRIT'; $rc = 2; }
elsif( @w ){ $r = 'WARN';  $rc = 1; }

print join(' / ', $r, @r, @c, @w), "| ", join(' ', @p), "\n";
exit $rc;


