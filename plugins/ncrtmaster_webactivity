#!/usr/bin/perl

use strict;

if( @ARGV < 5 ){
        print "usage: % $0 CONFDIR WORKDIR MEASURE HOST SERVICE\n";
        exit 1;
}

our $CONFDIR = $ARGV[0];
our $WORKDIR = $ARGV[1];
our $MEASURE = $ARGV[2];
our $VHOST = $ARGV[3];
our $VSERVICE = $ARGV[4];

####
sub ns ($) {
	return sprintf "%.2fs", $_[0];
}

sub nB ($) {
	return sprintf "%dB", $_[0];
}

#### load service conf.
my %param;
my $f = "$CONFDIR/agentless/webactivity.conf.$VHOST.$VSERVICE";
open my $h, '<', $f or do {
	die "$f: cannot open, stopped";
};
while( <$h> ){
	chomp;
	next if m"^\s*($|#)";
	die "$f:$.: illegal format, stopped" unless m"^(\w+)=(.*)";
	$param{$1} = $2;
}
close $h;

my $url = $param{URL};
unless( $url =~ m"^http(s)?://(?:([^\s:]+):([^\s\@]*)\@)?([-0-9a-zA-Z.]+)(?::(\d+))?((/[^\s\?]*)?(?:\?(\S*))?)$" ){
	die "url: invalid format, stopped";
}
my $secure = $1 ? 1 : undef;
my $user   = $param{User}     ne "" ? $param{User}     : $2 ne "" ? $2 : undef;
my $passwd = $param{Password} ne "" ? $param{Password} : $3 ne "" ? $3 : undef;
my $host   = $4;
my $port   = $5 || ($secure ? 443 : 80);
my $path_query = $6;
my $path = $7;
my $query = $8;
my $wantstatus = $param{StatusCode};
my $options = $param{ChkCmdOptions};

my $args = "-H $host -p $port";
$args .= " -S" if $secure;
$args .= " -a \Q$user:$passwd\E" if $user;
$args .= " -u \Q$path_query\E" if $path_query;
if( $options ){
	foreach my $option ( split m"\s+", $options ){
		$args .= " \Q$option\E";
	}
}

my %d;
my $f = "/usr/lib/nagios/plugins/check_http";
open my $h, '-|', "$f $args" or do {
	die "$f: cannot execute, stopped";
};
my $r = <$h>;
close $h;
chomp $r;
$r =~ m"^
	HTTP\ (
	    (?:
		(OK|WARNING|CRITICAL):
		\ HTTP/\S+\ (\d+)\ (?:(.*)\ )?-\ (.*)
		\|time=(\d+\.\d+)s\;.*\ size=(\d+)B\;.*
	    ) |
	    CRITICAL\ -\ (.*)
	)
$"x or do {
	print "web[$VSERVICE]-size=-1\n";
	print "web[$VSERVICE]-elapsed=-1\n";
	print "web[$VSERVICE]-status=2\n";
	open my $h, '>>', "$WORKDIR/webactivity.log";
	print $h time, " ERROR: url=$url response=$r\n";
	close $h;
	exit 0;
};
my $result = $2;
my $statuscode = $3;
my $statusmessage = $4 || $8;
my $message = $5;
my $elapsed = ns $6;
my $size = nB $7;
my $status = $statuscode eq $wantstatus ? 0 : 1;
$status = 2 if $result eq "CRITICAL";
print "web[$VSERVICE]-size=$size\n";
print "web[$VSERVICE]-elapsed=$elapsed\n";
print "web[$VSERVICE]-status=$status\n";

exit 0;

